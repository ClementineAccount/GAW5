Let's make a game!
	name:Fishing Game
	by:Clem
	desc:This is a simple fishing game that I am working on between 6/11/2023 to 12/11/2023. This is my first ever Idle Game Maker game! The ocean image is from: https://libreshot.com/calm-sea/ and the fish graphics are from: https://captainskeleto.itch.io/tropical-fishing-assets-pixelart-pixel-art-sprite-tropical-fish-pack-for-rpg-fan
	created:6/11/2023
	updated:8/11/2023
	version:0.9.2

Settings
	background:https://media.githubusercontent.com/media/ClementineAccount/GAW5/main/ocean.jpg
	building cost refund:50%
	spritesheet:icons, 48 by 48, https://media.githubusercontent.com/media/ClementineAccount/GAW5/main/fishSheet.png
	stylesheet:https://pastebin.com/raw/BgW42Ytz


Layout
  use default



Buttons
	*fishingButton
        tag:button
		name:Go Fishing
		desc:Attempt to catch a fish with a [fishChance]% chance of catching any fish. You currently have to wait [cooldownCost] seconds between fishing attempts.
		on click:anim icon wobble
		on click:hookState is 1
		on click:if (cooldownCost > 0) hide fishingButton
		on catch:
			if (chance((fishChance)%)) 
				if (chance(25%))
					if (chance(85%))
						yield 1 blueFish
					else
						yield 1 yellowFish
					end
				else
					yield 1 redFish
				end
			end
		end
		on click:
			if ((firstAttempt) is 1)
				yield 1 redFish
				firstAttempt is 0
			else if (chance((fishChance)%)) 
				if (chance(25%))
					if (chance(85%))
						yield 1 blueFish
					else
						yield 1 yellowFish
					end
				else
					yield 1 redFish
				end
			end
		end
		icon:https://media.githubusercontent.com/media/ClementineAccount/GAW5/main/hook.png
		no text
		class:bigButton hasFlares
		icon class:shadowed
		tooltip origin:bottom
		tooltip class:red

	*flavorText
		name:flavor text item
		desc:Quirky flavor text that comments on things just like in Cookie Clicker
		text:A nice day for fishing...
		on tick:hide flavorText
		no tooltip
		no click
		class:flavor-text

	*sellFishButton
		name:Sell Fish
		desc:Sell your fishes for money.
		on click:
			yield (redFish * (redFishPrice)) money
			redFish is 0
		end
		on click:
			yield ((blueFish) * (blueFishPrice)) money
			blueFish is 0
		end
		on click:
			yield ((yellowFish) * (yellowFishPrice)) money
			yellowFish is 0
		end
		on tick:
			hide sellFishButton
			if (redFish > 0)
				show sellFishButton
			else if (blueFish > 0)
				show sellFishButton
			else if (yellowFish > 0)
				show sellFishButton 
			end
		end
		text:Sell fish at the market.
		tooltip origin:bottom
		tooltip class:red



	*timeTicker
		desc:Increases the timer when the fishingButton is clicked
		hidden
		on tick:if (hookState is 1) yield 1 timeUnit
		on tick:
			if (timeUnit >= (cooldownCost)) 
				show fishingButton
				hookState is 0
				timeUnit is 0
			end
		end

Resources

	*cooldownCost
		name:cooldown cost
		desc:How long it takes in seconds before you can try again
		start with:5
		hidden

	*redFishPrice
		name:price of red fish
		desc:The price of the red fish.
		start with:1
		hidden

	*redFish
		name:Red Tuna|Red Tunas
		desc:The Red Tuna is common and taken for granted by the population. If only we could convince the people to want them... really really want them. If so, we could sell them at a higher price as the demand increases. The Red Tuna currently fetches [redFishPrice] money at the market. 
		icon:icons[0,0]
		class:noBackground
		show earned

	*blueFishPrice
		name:price of the blue fish
		desc:The price of the blue fish
		start with:10
		hidden

	*blueFish
		name:Blue Trout|Blue Trouts
		desc:The Blue Trout sells well and fetches a decent price but because it does, every other fishing company is also seeking them. This makes them harder for you to catch. We need to find a way to catch more of them and for our competitors to catch less of them. The Blue Trout currently fetches [blueFishPrice] money at the market. 
		icon:icons[3,0]
		class:noBackground
		show earned

	*yellowFishPrice
		name:price of the yellow fish
		desc:The price of the yellow fish
		start with:100
		hidden

	*yellowFish
		name:Golden Roughy|Golden Roughies
		desc:The Golden Roughy is a rare and endangered fish species, which makes catching and smuggling them difficult. Maybe by working to twist the laws, you might be able to decrease your overhead by selling them through more legal channels. The Golden Roughy currently fetches [yellowFishPrice] money at the market... after paying hefty taxes and bribes.
		icon:icons[2,0]
		class:noBackground
		show earned		
	
	*money|money
		name:money|money
		desc:Money can be exchanged for goods and services.
		icon:icons[1,0]
		class:noBackground
		show earned
	*timeUnit
		name:timeUnit
		desc:Time unit for when to cast a net again. Hidden resource for a timer
		hidden
	*hookState
		name:hook state
		desc:The state of the hook. 0 = can cast. 1 = cannot cast. Maybe there'd be more
		hidden
	*fishChance
		name:fish chance
		desc:The chance of catching a fish at all
		start with:20
		hidden

	*firstAttempt
		name:First Attempt Flag
		desc:This forces the chance to be 100% when the player just starts the game
		start with:1
		hidden

Buildings
	*fisherman|fishermen
		name:Fisherman|Fishermen
		desc:Hire a local fisherman. <//><b>Effect:</b><.>Catch 1 [n:redFish] every 5 seconds.
		cost:100 money
		on tick:yield 0.2 redFish

	*boat
		name:Fishing Boat|Fishing Boats
		desc:Purchase a fishing boat. <//><b>Effect:</b><.>Catch 1 [n:blueFish] every 10 seconds.
		cost:1000 money
		on tick:yield 0.1 blueFish

Upgrades
	*lowerCooldown
		name:Better Hook 1
		desc:Decreases the amount of time you need to wait between fishing by 1 second
		cost:3 money
		passive:cooldownCost is 4

	*lowerCooldown2
		name:Better Hook 2
		desc:Decreases the amount of time you need to wait between fishing by another 1 second
		cost:25 money
		req:have lowerCooldown
		passive:cooldownCost is 3

	*lowerCooldown3
		name:Better Hook 3
		desc:Decreases the amount of time you need to wait between fishing by another 1 second
		cost:100 money
		req:have lowerCooldown2
		passive:cooldownCost is 2

	*lowerCooldown4
		name:Better Hook 4
		desc:Decreases the amount of time you need to wait between fishing by another 1 second
		cost:500 money
		req:have lowerCooldown3
		passive:cooldownCost is 1

	*lowerCooldown5
		name:Automatic Hook
		desc:Decreases the cooldown time between fishing to 0 seconds
		cost:100000 money
		req:have lowerCooldown4
		passive:cooldownCost is 0

	*betterBait1
		name:Better Bait 1
		desc:Increases the chance of catching any fish per fishing attempt to 40%
		cost:3 money
		passive:fishChance is 40

	*betterBait2
		name:Better Bait 2
		desc:Increases the chance of catching any fish per fishing attempt to 60%
		cost:20 money
		req:have betterBait1
		passive:fishChance is 60

	*betterBait3
		name:Better Bait 3
		desc:Increases the chance of catching any fish per fishing attempt to 80%
		cost:200 money
		req:have betterBait2
		passive:fishChance is 80

	*betterBait4
		name:Chemically Enhanced Bait
		desc:Increases the chance of catching any fish per fishing attempt to 100%
		cost:10000 money
		req:have betterBait3
		passive:fishChance is 100
